apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "site.hanschen.pretty"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('../keystore/release.keystore')
            storePassword getProperty('store.password', 'STORE_PASSWORD')
            keyAlias getProperty('store.key.alias', 'KEY_ALIAS')
            keyPassword getProperty('store.key.password', 'KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def oldFile = output.outputFile
            def releaseApkName = String.format('%s_%s_%s_%s.apk', 'Pretty', getVersionName(), getDate(), variant.buildType.name);
            output.outputFile = new File(oldFile.parent, releaseApkName)
        }
    }
}

def getDate() {
    return new Date().format('yyyyMMdd', TimeZone.getTimeZone('GMT+8'))
}

def getVersionName() {
    return 'v' + android.defaultConfig.versionName
}

def getProperty(String propertyName, String envName) {
    try {
        def property = project.hasProperty(propertyName) ? project.property(propertyName) : System.getenv(envName)
        if (property == null) {
            throw new NullPointerException();
        }
        return property
    } catch (Throwable ignored) {
        def message = String.format('********************************************************************************\n' +
                'You need define %s in gradle.properties or set environment variable: %s\n' +
                '********************************************************************************\n', propertyName, envName)
        throw new RuntimeException(message)
    }
}

greendao {
    schemaVersion 1
    daoPackage 'site.hanschen.pretty.db.gen'
    targetGenDir 'src/main/java'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:support-v13:25.0.1'

    compile 'site.hanschen:common:1.0.9'

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.9'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'org.greenrobot:greendao:3.2.2'
}
